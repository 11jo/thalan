/*************************************************************************
 * Thalantyr Item Upgrade Mod
 **************************************************************************/
BACKUP ~thalan/backup~  
AUTHOR "crevs.daak@gmail.com" /* ~serdrick@yandex.ru~ I think it would be
				wise to put my email if I'm keeper of the mod */
VERSION ~v4.0~
README "thalan/readme.txt" "thalan/readme_%LANGUAGE%.txt"

ALWAYS
  ACTION_IF GAME_IS "tutu tutu_totsc" BEGIN //Tutu
    PRINT @90010
    INCLUDE ~thalan/lib/g3_tutu_cpmvars.tpa~
  END ELSE
  ACTION_IF GAME_IS bgt BEGIN //BGT
    PRINT @90011
    INCLUDE ~thalan/lib/g3_bgt_cpmvars.tpa~
  END ELSE
  ACTION_IF GAME_IS totsc BEGIN //BG1:TotSC and not IWD1
    PRINT @90012
    INCLUDE ~thalan/lib/g3_bg_cpmvars.tpa~
  END ELSE
  ACTION_IF GAME_IS bgee BEGIN
    PRINT @90014
    INCLUDE ~thalan/lib/g3_bgee_cpmvars.tpa~
  END ELSE
  ACTION_IF GAME_IS eet BEGIN
    PRINT @90015
    INCLUDE ~thalan/lib/eet_cpmvars.tpa~
  END

  ACTION_IF "%LANGUAGE%" STR_CMP "english" BEGIN
    ACTION_DEFINE_ARRAY conv BEGIN
      game
      thalan
    END

    LAF HANDLE_CHARSETS
      STR_VAR
        tra_path = "thalan/tra"
        convert_array = conv
    END
  END

  LOAD_TRA "thalan/tra/%LANGUAGE%/game.tra"
END

LANGUAGE ~English~
         ~english~
         ~thalan/tra/english/setup.tra~
	 ~thalan/tra/english/thalan.tra~
         
LANGUAGE ~Russian~ 
         ~russian~
         ~thalan/tra/russian/setup-%WEIDU_OS%.tra~
         
LANGUAGE ~Francais (Traduit par Deratiseur)~
         ~french~ 
         ~thalan/tra/french/setup.tra~
         
LANGUAGE ~Deutsch (Uebersetzung von Leonardo Watson)~
         ~german~
         ~thalan/tra/german/setup.tra~

LANGUAGE ~Espanol (traducido por Jondalar)~
         ~castellano~
         ~thalan/tra/castellano/setup.tra~
 
LANGUAGE ~Italiano (by Ilot)~
         ~italian~
         ~thalan/tra/italian/setup.tra~

LANGUAGE ~Polski (tlumaczenie: morgan)~
         ~polish~
         ~thalan/tra/polish/setup.tra~

BEGIN @0
REQUIRE_PREDICATE GAME_IS "bgee eet tutu tutu_totsc totsc bgt" @90013

//Storage bags - not implemented for BG:TotSC
ACTION_IF !GAME_IS totsc BEGIN
  COPY ~thalan/Itm/thbag01.itm~ ~override/thbag01.itm~
  COPY ~thalan/Itm/thbag02.itm~ ~override/thbag02.itm~
  COPY ~thalan/Itm/thbag03.itm~ ~override/thbag03.itm~
  COPY ~thalan/Itm/thbag04.itm~ ~override/thbag04.itm~
  COPY ~thalan/Itm/thbag05.itm~ ~override/thbag05.itm~

  COPY ~thalan/Sto/thbag01.sto~ ~override/thbag01.sto~
  COPY ~thalan/Sto/thbag02.sto~ ~override/thbag02.sto~
  COPY ~thalan/Sto/thbag03.sto~ ~override/thbag03.sto~
  COPY ~thalan/Sto/thbag04.sto~ ~override/thbag04.sto~
  COPY ~thalan/Sto/thbag05.sto~ ~override/thbag05.sto~
END

COPY_EXISTING ~%tutu_scripth%IGHHEDG.sto~ override
  PATCH_IF !GAME_IS totsc BEGIN
    ADD_STORE_ITEM ~thbag01~  #0 #0 #0 ~IDENTIFIED~ #1
    ADD_STORE_ITEM ~thbag02~  #0 #0 #0 ~IDENTIFIED~ #1
    ADD_STORE_ITEM ~thbag03~  #0 #0 #0 ~IDENTIFIED~ #1
    ADD_STORE_ITEM ~thbag04~  #0 #0 #0 ~IDENTIFIED~ #1
    ADD_STORE_ITEM ~thbag05~  #0 #0 #0 ~IDENTIFIED~ #1
  END
  ADD_STORE_ITEM ~scrl5g~   #0 #0 #0 ~IDENTIFIED~ #2 //Remove Curse
BUT_ONLY

//Item Upgrade
ACTION_IF !GAME_IS totsc BEGIN

  //Items
  COPY ~thalan/Itm/thsw2h01.itm~ ~override/thsw2h01.itm~
    SAY NAME2 @1
    SAY DESC  @2
  COPY ~thalan/Itm/thdagg01.itm~ ~override/thdagg01.itm~
    SAY NAME2 @3
    SAY DESC  @4
  COPY ~thalan/Itm/thdagg02.itm~ ~override/thdagg02.itm~
    SAY NAME2 @5
    SAY DESC  @6
  COPY ~thalan/Itm/thring01.itm~ ~override/thring01.itm~
    SAY NAME2 @7
    SAY DESC  @8
  COPY ~thalan/Itm/thring02.itm~ ~override/thring02.itm~
    SAY NAME2 @9
    SAY DESC  @10
  COPY ~thalan/Itm/thbrac01.itm~ ~override/thbrac01.itm~
    SAY NAME2 @11
    SAY DESC  @12
  COPY ~thalan/Itm/thclck01.itm~ ~override/thclck01.itm~
    SAY NAME2 @13
    SAY DESC  @14
  COPY ~thalan/Itm/thclck02.itm~ ~override/thclck02.itm~
    SAY NAME2 @15
    SAY DESC  @16
  COPY ~thalan/Itm/thbelt01.itm~ ~override/thbelt01.itm~
    SAY NAME2 @17
    SAY DESC  @18
  COPY ~thalan/Itm/thsw1h01.itm~ ~override/thsw1h01.itm~
    SAY NAME2 @19
    SAY DESC  @20
  COPY ~thalan/Itm/thbow01.itm~ ~override/thbow01.itm~
    SAY NAME2 @21
    SAY DESC  @22
  COPY ~thalan/Itm/thboot01.itm~ ~override/thboot01.itm~
    SAY NAME2 @23
    SAY DESC  @24
  COPY ~thalan/Itm/thamul01.itm~ ~override/thamul01.itm~
    SAY NAME2 @25
    SAY DESC  @26
  COPY ~thalan/Itm/thhelm01.itm~ ~override/thhelm01.itm~
    SAY NAME2 @27
    SAY DESC  @28
  COPY ~thalan/Itm/thclck03.itm~ ~override/thclck03.itm~
    SAY NAME2 @29
    SAY DESC  @30
    
  
  //Dialogue
  COMPILE EVAL ~thalan/dlg/thalan.d~ USING "thalan/tra/%LANGUAGE%/thalan.tra"
END
ELSE BEGIN //BG:TotSC

  // Additional IDS entries for scripts and dialog compiling
  APPEND ~action.ids~ ~160 ApplySpellRES(S:ResRef*,O:Target*)~       UNLESS ~ApplySpellRES~
  APPEND ~action.ids~ ~181 ReallyForceSpellRES(S:ResRef*,O:Target*)~ UNLESS ~ReallyForceSpellRES~
  APPEND ~action.ids~ ~204 TakePartyItemNum(S:ResRef*,I:Num*)~       UNLESS ~TakePartyItemNum~

  APPEND ~trigger.ids~ ~0x407A NumItemsParty(S:ResRef*,I:Num*)~        UNLESS ~NumItemsParty~
  APPEND ~trigger.ids~ ~0x407B NumItemsPartyGT(S:ResRef*,I:Num*)~      UNLESS ~NumItemsPartyGT~
  APPEND ~trigger.ids~ ~0x407C NumItemsPartyLT(S:ResRef*,I:Num*)~      UNLESS ~NumItemsPartyLT~

  // Rest-check system in BG1 for "one per day" item's features
  ACTION_IF (FILE_CONTAINS_EVALUATED (~dplayer3.BCS~ ~"Z!FATIG1"~))
    THEN BEGIN
      COPY_EXISTING ~dplayer3.BCS~ ~override~
        DECOMPILE_BCS_TO_BAF
        REPLACE_TEXTUALLY ~ApplySpellRES("Z!FATIG1",Player1)~
                          ~ApplySpellRES("Z!FATIG1",Player1)
                           SetGlobal("thclck01_REFILL","GLOBAL",2)
                           SetGlobal("thsw2h01_REFILL","GLOBAL",1)~
        COMPILE_BAF_TO_BCS
    END
    ELSE BEGIN
      EXTEND_TOP ~dplayer3.BCS~ ~thalan/Itm_bg1/Z!PartyRested_add.BAF~
      COPY ~thalan/Itm_bg1/Z!FATIG1.SPL~ ~override~
    END
    
  //Items
  COPY ~thalan/Itm_bg1/thsw2h01.itm~ ~override/thsw2h01.itm~
    SAY NAME2     @101
    SAY DESC      @102
  COPY ~thalan/Itm_bg1/STATES.BAM~ ~override~
  COPY ~thalan/Itm_bg1/STATES2.BAM~ ~override~
  COPY ~thalan/Itm_bg1/SPCL321.SPL~ ~override~
    SAY NAME1     @131
  COPY ~thalan/Itm_bg1/SPCL321D.SPL~ ~override~
  APPEND ~STATDESC.2DA~ ~141     99999~ UNLESS ~141     ~
  COPY_EXISTING ~STATDESC.2DA~ ~override~
    REPLACE ~99999~ @131
  APPEND ~TOOLTIP.2DA~ ~thsw2h01        -1          99999          -1~ UNLESS ~thsw2h01~
  COPY_EXISTING ~TOOLTIP.2DA~ ~override~
    REPLACE ~99999~ @131
  EXTEND_BOTTOM ~dplayer3.BCS~ ~thalan/Itm_bg1/dplayer3_thsw2h01AD.BAF~
  COPY ~thalan/Itm_bg1/inv_cre.CRE~ ~override/thsw2h01.CRE~
    WRITE_ASCII SCRIPT_OVERRIDE ~thsw2h01~
  COMPILE ~thalan/Itm_bg1/thsw2h01.BAF~

  COPY ~thalan/Itm/thdagg01.itm~ ~override/thdagg01.itm~
    SAY NAME2     @103
    SAY DESC      @104
  COPY ~thalan/Itm_bg1/IDAGG24.BAM~ ~override~
 
  COPY ~thalan/Itm_bg1/thdagg02.itm~ ~override/thdagg02.itm~
    SAY NAME2     @105
    SAY DESC      @106
  COPY ~thalan/Itm_bg1/IDAGG17.BAM~ ~override~

  COPY ~thalan/Itm_bg1/thring01.itm~ ~override/thring01.itm~
    SAY NAME2     @107
    SAY DESC      @108
  COPY ~thalan/Itm_bg1/Iring09Y.BAM~ ~override~

  COPY ~thalan/Itm_bg1/thring02.itm~ ~override/thring02.itm~
    SAY NAME2     @109
    SAY DESC      @110
  COPY ~thalan/Itm_bg1/Iring09G.BAM~ ~override~

  COPY ~thalan/Itm/thbrac01.itm~ ~override/thbrac01.itm~
    SAY NAME2     @111
    SAY DESC      @112

  COPY ~thalan/Itm_bg1/thclck01.itm~ ~override/thclck01.itm~
    SAY NAME2     @113
    SAY DESC      @114
  APPEND ~TOOLTIP.2DA~ ~thclck01          12017        -1          -1~ UNLESS ~thclck01~
  EXTEND_BOTTOM ~dplayer3.BCS~ ~thalan/Itm_bg1/dplayer3_thclck01AD.BAF~
  COPY ~thalan/Itm_bg1/inv_cre.CRE~ ~override/thclck01.CRE~
    WRITE_ASCII SCRIPT_OVERRIDE ~thclck01~
  COMPILE ~thalan/Itm_bg1/thclck01.BAF~

  COPY ~thalan/Itm/thclck02.itm~ ~override/thclck02.itm~
    SAY NAME2     @115
    SAY DESC      @116

  COPY ~thalan/Itm/thbelt01.itm~ ~override/thbelt01.itm~
    SAY NAME2     @117
    SAY DESC      @118

  COPY ~thalan/Itm_bg1/thsw1h01.itm~ ~override/thsw1h01.itm~
    SAY NAME2     @119
    SAY DESC      @120

  COPY ~thalan/Itm/thbow01.itm~ ~override/thbow01.itm~
    SAY NAME2     @121
    SAY DESC      @122

  COPY ~thalan/Itm_bg1/thboot01.itm~ ~override/thboot01.itm~
    SAY NAME2     @123
    SAY DESC      @124

  COPY ~thalan/Itm/thamul01.itm~ ~override/thamul01.itm~
    SAY NAME2     @125
    SAY DESC      @126

  COPY ~thalan/Itm_bg1/thhelm01.itm~ ~override/thhelm01.itm~
    SAY NAME2     @127
    SAY DESC      @128

  COPY ~thalan/Itm/thclck03.itm~ ~override/thclck03.itm~
    SAY NAME2     @129
    SAY DESC      @130

  APPEND ~ITEMEXCL.2DA~ ~thamul01  1~ IF ~RING~ UNLESS ~thamul01~
  APPEND ~ITEMEXCL.2DA~ ~thclck01  1~ IF ~RING~ UNLESS ~thclck01~
  APPEND ~ITEMEXCL.2DA~ ~thring01  1~ IF ~RING~ UNLESS ~thring01~
  APPEND ~ITEMEXCL.2DA~ ~thring02  1~ IF ~RING~ UNLESS ~thring02~

  //Dialogue
  ACTION_IF "%LANGUAGE%" STR_EQ "polish" THEN BEGIN
    COMPILE ~thalan/dlg_bg1/thalanPL-BG1.d~ USING ~thalan/tra/polish/thalanPL-BG1.TRA~
  END
  ELSE BEGIN
    COMPILE ~thalan/dlg_bg1/thalan.d~ USING "thalan/tra/%LANGUAGE%/thalan.tra"
  END

  COPY ~thalan/Itm_bg1/inv_cre.CRE~ ~override/z!visual.CRE~
    WRITE_ASCII SCRIPT_OVERRIDE ~z!visual~
    SAY NAME1 #-1
    SAY NAME2 #-1
  COMPILE ~thalan/Itm_bg1/z!visual.BAF~
END